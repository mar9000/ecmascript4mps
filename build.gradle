// Versions.
ext.mpsVersion = '2020.2';
allprojects {
    // Force group and version, do not consider properties passed by jitpack (-Porg.mar9000).
    // This way artifacts created are the same locally and on jitpack,
    // nevertheless remember to commit new version on their own branch/tag.
    group = "org.mar9000.mps"
    version = "$mpsVersion.$versionSuffix"
}

// Gradle related settings.
buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.4.+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}
wrapper {
    gradleVersion '6.4'
}
apply plugin: 'base'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
configurations {
    mps
    junitAnt
}
// Configure mps-gradle-plugin.
import de.itemis.mps.gradle.*
def mpsDir = "$buildDir/mps"
def jdkHome = System.getProperty('java.home')
def buildScriptClasspath = project.configurations.junitAnt.fileCollection { true } +
        project.files("$jdkHome/lib/tools.jar")
def defaultScriptArgs = [
	'mps_home': mpsDir
]
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = defaultScriptArgs.collect { "-D$it.key=$it.value".toString() }
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = buildScriptClasspath
ext["itemis.mps.gradle.ant.defaultJavaExecutable"] = file("$jdkHome/bin/java")
ext.artifactsDir = file("$buildDir/artifacts")

// Build settings.
repositories {
    mavenLocal()
    maven {
		url 'https://projects.itemis.de/nexus/content/repositories/mbeddr'
	}
    mavenCentral()
}
dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
    junitAnt "org.apache.ant:ant-junit:1.10.9"
}
// MPS part.
task resolveMps(type: Copy) {
    dependsOn configurations.mps
    from {
        configurations.mps.resolve().collect { zipTree(it) }
    }
    into mpsDir
}
// ecmascript4mps part.
task buildEcmascript4mps(type: BuildLanguages) {
    dependsOn "resolveMps"
	script "$projectDir/build.xml"
}
build.dependsOn buildEcmascript4mps

// Publishing.
task packageEcmascript4mps(type: Zip, dependsOn: build) {
    description "ECMAScript implementation for JetBrains MPS"
    archiveBaseName = 'org.mar9000.mps.ecmascript4mps'
    from artifactsDir
    include 'ecmascript4mps/build.properties'
    include 'ecmascript4mps/ecmascript4mps/**'
}
publishing {
    publications {
        ecmascript4mps(MavenPublication) {
            groupId "$group"
            artifactId 'ecmascript4mps'
            version "$version"
            artifact packageEcmascript4mps
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }
    }
}
// Bintray.
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['ecmascript4mps']
    pkg {
        repo = 'mar9000-public'
        name = 'ecmascript4mps'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mar9000/ecmascript4mps.git'
        version {
            name = "$version"
            released  = new Date()
        }
    }
}
